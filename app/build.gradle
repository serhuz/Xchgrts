import java.text.SimpleDateFormat

plugins {
    alias libs.plugins.android.application
    id 'kotlin-android'
    id 'com.google.devtools.ksp'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.android.gms.oss-licenses-plugin'
    id 'dagger.hilt.android.plugin'
    alias libs.plugins.compose.compiler
    alias libs.plugins.detekt.plugin
}

apply from: '../common/common.gradle'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

static def getDate() {
    def df = new SimpleDateFormat("yyyy.MM.dd-H'h'mm")
    df.setTimeZone(TimeZone.getTimeZone("Europe/Kiev"))
    return df.format(new Date())
}

android {
    namespace = 'xyz.randomcode.xchgrts'
    compileSdkVersion 36

    defaultConfig {
        applicationId "xyz.randomcode.xchgrts"
        versionCode 6
        versionName "1.0"

        buildConfigField "String", "UPDATE_WORK_NAME", '"updater"'
        buildConfigField "String", "UPDATE_WORK_TAG", '"update_rates"'

        setProperty("archivesBaseName", "app-${versionName}_${versionCode}-${getDate()}")
    }

    buildFeatures {
        compose true
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile rootProject.file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.dbg'
            versionNameSuffix '-dbg'
        }
        release {
            debuggable false
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

detekt {
    config.setFrom(file(rootProject.file("common/detekt.yml")))
    buildUponDefaultConfig = false
    autoCorrect = false
}

dependencies {
    implementation project(':domain')

    implementation libs.moshi
    ksp libs.moshi.codegen

    implementation libs.bundles.retrofit
    implementation libs.bundles.okhttp

    implementation libs.work
    implementation libs.preference
    implementation libs.annotation
    implementation libs.material
    implementation libs.datastore.preferences

    implementation libs.worldCountryData

    implementation libs.play.services.oss.licenses

    implementation libs.lifecycle.viewmodel.ktx
    implementation libs.lifecycle.viewmodel.savedstate
    implementation libs.lifecycle.livedata.ktx
    implementation libs.lifecycle.common.java8
    implementation libs.lifecycle.runtime.ktx

    implementation libs.hilt.android
    implementation libs.hilt.work
    ksp libs.hilt.compiler
    ksp libs.hilt.compiler.androidx

    implementation libs.recyclerview

    implementation libs.core.ktx
    implementation libs.appcompat
    implementation libs.ui.text.google.fonts

    implementation platform(libs.firebase.bom)
    implementation libs.bundles.firebase

    def composeBom = platform(libs.compose.bom)
    implementation composeBom
    androidTestImplementation composeBom

    implementation libs.material3

    implementation libs.glance
    implementation libs.glance.appwidget

    // Android Studio Preview support
    implementation libs.ui.tooling.preview
    debugImplementation libs.ui.tooling

    implementation libs.adaptive

    // Integration with activities
    implementation libs.activity.compose
    // Integration with ViewModels
    implementation libs.lifecycle.viewmodel.compose
    // Integration with LiveData
    implementation libs.runtime.livedata
}
